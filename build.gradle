plugins {
    id "fabric-loom" version "1.4.4"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }

    mavenCentral()
}

dependencies {
    def library = { id ->
        implementation id
        shadow id
    } as Object

    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor Client
    modImplementation "meteordevelopment:meteor-client:${project.meteor_version}"

    // Other Libraries
    library "meteordevelopment:discord-ipc:${project.discordrpc_version}"
}

jar {
    manifest {
        attributes("Main-Class": "cally72jhb.addon.Main")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
        filter { line -> line.replace("@mc_version@", project.minecraft_version) }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}
